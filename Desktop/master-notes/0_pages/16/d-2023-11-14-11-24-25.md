---
creation-date: 2023-11-14T11:24:25
catalogs:
  - c-2023-11-14-11-24-25
type: walkthrough
title: Deploying Azure Container Instance
aliases:
  - ACI
tags:
  - azure
  - containers
  - terraform
---

# üìì Deploying Azure Container Instance

![[3_hidden/_images/Pasted image 20231114200027.png]]
Deploying container application from scratch contains mainly 3 steps: 
1. deploy Resource Group & private Azure Container Registry (ACR)
2. build the image from dockerfile and push it to ACR
3. deploy Azure Container Instance to host container from ACR's image

---
## 1. Create Azure Container Registry (ACR)

*ACR* is used for registering and storing the docker's images in Azure. This is similar to open source service such as Dockerhub (see [[0_pages/05/d-2023-10-13-21-50-14|How does Docker works?]]). Azure allows images pull from both Dockerhub and private ACR. 

In our `acr/main.tf`, we defines our ACR resources and deploy it:

```terraform
// Setup the initial resource group if we don't have any
// this provides a organisational grouping of all relevant resources

resource "azurerm_resource_group" "rg" {
  location = "swedencentral"
  name = "${var.resource_group_prefix}-aci"
}

// ----------- [ Create Container Registry ] ------------

resource "azurerm_container_registry" "acr" {
  name                = "containerRegistry0131234123"
  resource_group_name = azurerm_resource_group.rg.name
  location            = azurerm_resource_group.rg.location
  sku                 = "Premium" // allow private
  admin_enabled       = true  // allow admin to push image
}
```

---
## 2. Push Image to ACR

Locally, we have a `dockerfile` that can build into an image. Instead of publishing the image to Dockerhub, we publish it to our private ACR. We can use Azure CLI to do this:

```shell
# login az to allow interaction with ACR
az login
az acr login --name <ACRName>

# build our image locally and push to ACR
export NAMESPACE="mynamespace"
export IMAGE_TAG="my-tag"
az acr build -t $NAMESPACE/$IMAGE_TAG:latest -r <ACRName>
```

Each image push to ACR requires two part: 
- `NAMESPACE` - is the specific namespace for ACR 
- `IMAGE_TAG` - is the tag we normally defined when using `docker build`

An image with URI `<ACRName>.azurecr.io/$NAMESPACE/$IMAGE_TAG` would exist in ACR and ready to be deployed in our ACI container group. 

> [!NOTE] Dockerfile
> Unlike many interactive Dockerfile, Dockerfile for ACR needs to be able to attach and run in the background, e.g., `CMD sleep infinity`

---
## 3. Create Private Container Group

Next we need to define a ACI Container Group to host container application. This is similar to [[0_pages/78/d-2023-11-13-11-23-50|hosting Azure VM]], which requires: 

- Virtual Network - to provide a network layer for network traffics to the Container Group
- Subnet - to allow more fine grind logical group of applications
- *Container Group* - the container deployment set, this is similar to composing containers

In `aci/main.tf`, we define our resource (note: we reuse the defined Resource Group for ACI resources aswell):

```terraform
// create virtual network
resource "azurerm_virtual_network" "aci_vnet" {
  name = "myVNet"
  address_space = ["10.0.0.0/16"]
  location = var.rg.location
  resource_group_name =  var.rg.name
}

// create subnet for the private container
resource "azurerm_subnet" "aci_subnet" {
  name                 = "mySubnet"
  resource_group_name  = var.rg.name
  virtual_network_name = azurerm_virtual_network.aci_vnet.name
  address_prefixes     = ["10.0.1.0/24"]

  // allow the container network to access the subnet's networks
  delegation {
    name = "aci_delegation"
    service_delegation {
      name = "Microsoft.ContainerInstance/containerGroups"
    }
  }
}

// allow container name to be generated randomly
resource "random_string" "rand" {
  length  = 25
  lower   = true
  upper   = false
  special = false
}

// Containers Group
resource "azurerm_container_group" "container" {
  name                = "group-${random_string.rand.result}"
  location            = var.rg.location
  resource_group_name = var.rg.name
  ip_address_type     = "Private"  // prevent Public IP access
  subnet_ids = [ azurerm_subnet.aci_subnet.id ]
  os_type             = "Linux"
  restart_policy      = var.restart_policy

  container {
    name   = "container-${random_string.rand.result}"
    image  = var.image
    cpu    = var.cpu_cores
    memory = var.memory_in_gb

    ports {
      port     = var.port
      protocol = "TCP"
    }
  }

	// require login to pull the ACR image
  image_registry_credential {
    username = var.acr_username
    password = var.acr_password
    server = "containerregistry0131234123.azurecr.io"
  }
}
```

In here, we define a private container that isn't expose to the internet with a public IP address. We also limits the ACR interaction in this deployment process to requires ACR admin credentials. 

Before using `terraform apply` to deploy, we need to acquire the credentials using CLI: 

```shell
az acr credential show --name <registryName>
```

---
## 4. Interacting with the Container

We can get into the container using CLI: 

```shell
az container \
	exec --exec-command "bash" \
	-g <resourceGroup> --name <containerName>
```

---
## ‚ÑπÔ∏è¬† Resources
- [Deploy container group to Azure virtual network - Azure Container Instances](https://learn.microsoft.com/en-us/azure/container-instances/container-instances-vnet)
- [Terraform Container Registry](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/container_registry)